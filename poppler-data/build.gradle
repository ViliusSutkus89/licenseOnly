plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '0.4.9'
    }
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = project.name
                    description = 'Encoding files for use with poppler.'
                    url = 'https://poppler.freedesktop.org'
                    packaging = 'aar'
                    inceptionYear = '2006'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://cgit.freedesktop.org/poppler/poppler-data/tree/COPYING?h=POPPLER_DATA_0_4_9'
                            distribution = 'repo'
                        }
                        license {
                            name = 'COPYING.adobe'
                            url = 'https://cgit.freedesktop.org/poppler/poppler-data/tree/COPYING.adobe?h=POPPLER_DATA_0_4_9'
                            distribution = 'repo'
                        }
                        license {
                            name = 'GPLv2'
                            url = 'https://cgit.freedesktop.org/poppler/poppler-data/tree/COPYING.gpl2?h=POPPLER_DATA_0_4_9'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://cgit.freedesktop.org/poppler/poppler-data'
                        connection = 'https://anongit.freedesktop.org/git/poppler/poppler-data.git'
                    }
                    developers {
                        developer {
                            id = 'tsdgeos'
                            name = 'Albert Astals Cid'
                            email = 'aacid@kde.org'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}

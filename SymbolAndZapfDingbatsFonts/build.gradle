plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '4.03'
    }
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
}


task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = project.name
                    description = 'Symbol and Zapf Dingbats Type 1 fonts'
                    url = 'http://www.xpdfreader.com/download.html'
                    packaging = 'aar'
                    inceptionYear = '1995'
                    licenses {
                        license {
                            name = 'GPLv2'
                            url = 'https://www.gnu.org/licenses/gpl-2.0.html'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
